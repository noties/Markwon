apply plugin: 'com.android.application'

android {

    compileSdkVersion config['compile-sdk']
    buildToolsVersion config['build-tools']

    defaultConfig {
        applicationId "ru.noties.markwon.sample"
        minSdkVersion config['min-sdk']
        targetSdkVersion config['target-sdk']
        versionCode 1
        versionName version
        setProperty("archivesBaseName", "markwon-sample-$versionName")

        resConfig 'en'
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize '5g'
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            // let's use different res directory so sample will have _isolated_ resources from others
            res.srcDirs += [ './src/main/recycler/res' ]
            assets.srcDirs += ['./src/main/recycler/assets']
        }
    }
}

dependencies {

    implementation project(':markwon')
    implementation project(':markwon-ext-strikethrough')
    implementation project(':markwon-ext-tables')
    implementation project(':markwon-ext-tasklist')
    implementation project(':markwon-html')
    implementation project(':markwon-image-gif')
    implementation project(':markwon-image-svg')
    implementation project(':markwon-syntax-highlight')
    implementation project(':markwon-recycler')

    deps.with {
        implementation it['support-recycler-view']
        implementation it['okhttp']
        implementation it['prism4j']
        implementation it['debug']
        implementation it['adapt']
    }

    deps['annotationProcessor'].with {
        annotationProcessor it['prism4j-bundler']
    }

    deps['test'].with {
        testImplementation it['junit']
        testImplementation it['robolectric']
        testImplementation it['mockito']
    }
}
