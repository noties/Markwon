buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

allprojects {
    repositories {
        if (project.hasProperty('LOCAL_MAVEN_URL')) {
            maven { url LOCAL_MAVEN_URL }
        }
        jcenter()
        google()
    }
    version = VERSION_NAME
    group = GROUP
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task wrapper(type: Wrapper) {
    gradleVersion '4.8.1'
    distributionType 'all'
}

if (hasProperty('local')) {
    if (!hasProperty('LOCAL_MAVEN_URL')) {
        throw new RuntimeException('Cannot publish to local maven as no such property exists: LOCAL_MAVEN_URL')
    }
    ext.RELEASE_REPOSITORY_URL = LOCAL_MAVEN_URL
    ext.SNAPSHOT_REPOSITORY_URL = LOCAL_MAVEN_URL
}

ext {

    // Config
    BUILD_TOOLS = '27.0.3'
    TARGET_SDK = 27
    MIN_SDK = 16

    // Dependencies
    final def supportVersion = '27.1.1'
    SUPPORT_ANNOTATIONS = "com.android.support:support-annotations:$supportVersion"
    SUPPORT_APP_COMPAT = "com.android.support:appcompat-v7:$supportVersion"

    final def commonMarkVersion = '0.11.0'
    COMMON_MARK = "com.atlassian.commonmark:commonmark:$commonMarkVersion"
    COMMON_MARK_STRIKETHROUGHT = "com.atlassian.commonmark:commonmark-ext-gfm-strikethrough:$commonMarkVersion"
    COMMON_MARK_TABLE = "com.atlassian.commonmark:commonmark-ext-gfm-tables:$commonMarkVersion"

    ANDROID_SVG = 'com.caverock:androidsvg:1.2.1'
    ANDROID_GIF = 'pl.droidsonroids.gif:android-gif-drawable:1.2.14'
    OK_HTTP = 'com.squareup.okhttp3:okhttp:3.9.0'

    PRISM_4J = 'ru.noties:prism4j:1.1.0'
    PRISM_4J_BUNDLER = 'ru.noties:prism4j-bundler:1.1.0'

    JUNIT = 'junit:junit:4.12'
    ROBOLECTRIC = 'org.robolectric:robolectric:3.8'
}
